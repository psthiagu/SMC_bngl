
begin model 

begin molecule types
	A(loc~B~P~BM,cd3,cd123)
	Tc4(loc~B~BM,ex~0~1,syn,act~0~1,adh~0~1) 
    Tc8(loc~B~BM,ex~0~1,syn,act~0~1,adh~0~1)
   	CD34(loc~B~BM,a)
    CD38(loc~B~BM,a)
    Leuk(loc~B~BM,syn) 
	CD123(loc~B~BM,a)
	IFNg(loc~B~BM)
	IL2(loc~B~BM)
	IL6(loc~B~BM)
	IL10(loc~B~BM)
end molecule types

begin parameters
    # Constants
	NA	6.022e23
	mass 3.9 # average weight of monkey in kg
	molwt 1.26e5
	
	# Infusion properties
	dose 0.1  # ug/kg dose administered over one hour infusion (value and sim protocol set below).
	          # NOTE: 1 ug/kg = 1.864e13 molec / monkey = 17.86 ng/mL
	          #       Kd's for monkey are 4284 ng/mL for CD3 and 38.68 ng/mL for CD123
	duration 1/24 # duration of infusion in days
	doserate dose/duration*mass*NA*1e-6/molwt # Infusion rate [molec/day]
	eps 1e-10
	
	# Animal PK properties
	CL   3.832473e+03 #83.0*mass # mL/day/monkey # Clearance rate from blood
	V_B  218.4 #56.0*mass # mL/monkey    # Volume of blood compartment
	Q    5.767320e+03 #427*mass  # mL/day/monkey # Transport rate between blood and periphery
	V_P  702.0 #180*mass  # mL/monkey    # Volume of peripheral comparment
	V_BM  35.1 #9*mass # mL/monkey    # Volume of bone marrow compartment

	# Flux partioning (not fit directly)
	q_P Q*V_P/(V_P + V_BM)
	q_BM Q*V_BM/(V_P + V_BM)
	
	# T cell properties
	tc4_dens_B  1.974e6  # [cells/ml] Resting cd4T cell density in blood # bing # initial value computed from data
	tc4_dens_BM 2.808e6  # [cells/ml] Resting cd4T cell density in bone marrow	#initial values computed from data
    tc8_dens_B  9.188e5  # [cells/ml] Resting cd8T cell density in blood # bing # initial value computed from data
	tc8_dens_BM 1.469e6  # [cells/ml] Resting cd8T cell density in bone marrow	#initial values computed from data
	rho3_init   6e4	# CD3 density, per T cell (El Hentati: 3.5e4-8.5e4; Offner: 6.7e4)
	k_tc4_log   3.603764e-01    # [1/day] growth rate for naive CD4 T cell growth with logistic model
    k_tc8_log   2.575366e+00    # [1/day] growth rate for naive CD8 T cell growth with logistic model

    Leuk_dens_B    9.600e4 # [cells/ml] # calculated average density from data
    Leuk_dens_BM   2.422e5 # [cells/ml] # calculated average density from data
	rho123_init	   1.45e4 # [/cell] Leuk CD123 density; setting it to be roughly 50% of the density for pDC cells.
	k_LeukB_log    10  # 6.04855e+00 # [1/day] growth rate for Leuk's growth with logistic model; blood
    k_LeukBM_log   10  # 2.58658e+01[1/day] growth rate for Leuk's growth with logistic model; BM
    k_Leuk_deg_B   0.1
    k_Leuk_deg_BM  0.1
    k_Leuk_syn_B   Leuk_dens_B*k_Leuk_deg_B
    k_Leuk_syn_BM  Leuk_dens_BM*k_Leuk_deg_BM
    

	# Asset binding parameters from Sanofi ONVT0141 Table 1 (Cynomolgus parameters used)
    # CD3 binding
	kp3 5.2225e-12 #3.64e4*(3600*24)*1000/NA # 1/M 1/s -> mL/molec 1/day # Divide this number by appropriate volume for binding constant
	km3 105.408 #1.22e-3*(3600*24) # 1/day
	# CD123 binding
	kp123 2.89817336433079e-11 #2.02e5*(3600*24)*1000/NA # 1/M 1/s -> mL/molec 1/day # Divide this number by appropriate volume for binding constant
	km123 5.42592 #6.28e-5*(3600*24) # 1/day
	
	# Receptor trafficking and turnover parameters
	kd3 0.3 # [1/day] Receptor degradation (Page et al.)
	kb3 (kd3 * rho3_init) # [#/cell/day] Receptor synthesis that gives rho3_init steady-state
	ki3 50 # [1/day] Bound receptor internalization rate constant (Page et al.)

	# Synapse formation
	kf 	4.36  # Cell encounter rate
	nb  13.15 # Number of bridges in synapse

	# T cell activation and proliferation
	k4_act 1.586623e+02 # Rate at which cd4T cells get activated when synapsed
    k8_act 2.481135e+02 # Rate at which cd8T cells get activated when synapsed
	k_tcb  5.3e-2 # Rate of active T cell proliferation
	k4_ex  8.2e-2 # Rate of active T cell exhaustion (exhausted T cells secrete cytokines and kill at a reduced rate)
    k8_ex  1.11e-1 
        
	# Cytotoxcity
	k4_kill  16   # Rate of killing by activated cd4T cells (inactive T cells can't kill)
    k8_kill  33   # Rate of killing by activated cd8T cells (inactive T cells can't kill)
	k4_tox   0.12 # Rate at which active cd4T cells die in synapse
    k8_tox   0.12 # Rate at which active cd8T cells die in synapse
	kr 	     10 # 2600  # [1/day] Synapse dissociation rate (lifetime ~ hours)

	# T-cell migration between blood and bone marrow
	k_adh 20 # 270 # the expression of adhesion molecules on T_cells bound to A_blood molecules; \ 
               # wild guess initial value; to be estimated;
	k_migrate_B  9e3
	k_migrate_BM 0.4 # 0.03 # T-cell revival due to T_cell trafficking from BM to blood; wild guess
    thresh_adh 10
    thresh_rev 90

    # cytokine secretion and degradation rates
	kf_ifng 9.798275e+05
	kr_ifng 2.174206e+01
	kf_il2  1.138883e+05
	kr_il2  10.095219e+00
	kf_il6  2.140300e+06
	kr_il6  2.399406e+02
    kf_il10 36.600955e+05
    kr_il10 0.530465e+01
        
end parameters

begin species
  Tc4(loc~B,syn,act~0,ex~0,adh~0)  tc4_dens_B*V_B
  Tc8(loc~B,syn,act~0,ex~0,adh~0)  tc8_dens_B*V_B
  Tc4(loc~BM,syn,act~0,ex~0,adh~0) tc4_dens_BM*V_BM # Source: TSK data.
  Tc8(loc~BM,syn,act~0,ex~0,adh~0) tc8_dens_BM*V_BM # Source: TSK data.
  CD34(loc~B,a)                    tc4_dens_B*V_B*rho3_init  
  CD38(loc~B,a)                    tc8_dens_B*V_B*rho3_init 
  CD34(loc~BM,a)                   tc4_dens_BM*V_BM*rho3_init  
  CD38(loc~BM,a)                   tc8_dens_BM*V_BM*rho3_init  
  Leuk(loc~B,syn)                  Leuk_dens_B*V_B
  Leuk(loc~BM,syn)                 Leuk_dens_BM*V_BM
  CD123(loc~B,a)                   Leuk_dens_B*V_B*rho123_init
  CD123(loc~BM,a)                  Leuk_dens_BM*V_BM*rho123_init
  
end species

begin observables
	Molecules A_B A(loc~B)
	Molecules A_P A(loc~P)
	Molecules A_BM A(loc~BM)
	Molecules Tc4_naive_B Tc4(loc~B,syn,act~0,ex~0,adh~0)
        Molecules Tc8_naive_B Tc8(loc~B,syn,act~0,ex~0,adh~0)
	Molecules Tc4_naive_BM Tc4(loc~BM,syn,act~0,ex~0,adh~0)
        Molecules Tc8_naive_BM Tc8(loc~BM,syn,act~0,ex~0,adh~0)
	Molecules Tc4_B Tc4(loc~B)
        Molecules Tc8_B Tc8(loc~B)
	Molecules Tc4_BM Tc4(loc~BM)
        Molecules Tc8_BM Tc8(loc~BM)
    Molecules Tc4_free_B Tc4(loc~B,syn)
    Molecules Tc8_free_B Tc8(loc~B,syn)
    Molecules Tc4_free_BM Tc4(loc~BM,syn)
    Molecules Tc8_free_BM Tc8(loc~BM,syn)
    Molecules Leuk_B Leuk(loc~B) 
    Molecules Leuk_BM Leuk(loc~BM) 
    Molecules Leuk_free_B Leuk(loc~B,syn) 
    Molecules Leuk_free_BM Leuk(loc~BM,syn)
    Molecules Tc4_free_act_B Tc4(loc~B,syn,act~1)
    Molecules Tc8_free_act_B Tc8(loc~B,syn,act~1)
    Molecules Tc4_free_act_BM Tc4(loc~BM,syn,act~1)
    Molecules Tc8_free_act_BM Tc8(loc~BM,syn,act~1)
    Molecules Tc4_free_adh_BM Tc4(loc~BM,syn,adh~1)
    Molecules Tc8_free_adh_BM Tc8(loc~BM,syn,adh~1)
    # Receptor-related
    # CD3
	Molecules CD34_B CD34(loc~B)
        Molecules CD38_B CD38(loc~B)
	Molecules CD34_BM CD34(loc~BM)
        Molecules CD38_BM CD38(loc~BM)
	Molecules CD34_free_B CD34(loc~B,a)
        Molecules CD38_free_B CD38(loc~B,a)
	Molecules CD34_free_BM CD34(loc~BM,a)
        Molecules CD38_free_BM CD38(loc~BM,a)
	Molecules CD34_bound_B CD34(loc~B,a!1).A(cd3!1,cd123)
        Molecules CD38_bound_B CD38(loc~B,a!1).A(cd3!1,cd123)
	Molecules CD34_bound_BM CD34(loc~BM,a!1).A(cd3!1,cd123)
        Molecules CD38_bound_BM CD38(loc~BM,a!1).A(cd3!1,cd123)
	Molecules CD123_B CD123(loc~B)
	Molecules CD123_BM CD123(loc~BM)
	Molecules CD123_free_B CD123(loc~B,a)
	Molecules CD123_free_BM CD123(loc~BM,a)
	Molecules CD123_bound_B CD123(loc~B,a!1).A(cd123!1,cd3)
	Molecules CD123_bound_BM CD123(loc~BM,a!1).A(cd123!1,cd3)

	# Synapse related
	Molecules Leuk4_synapse_B Leuk(loc~B,syn!1).Tc4(syn!1)
        Molecules Leuk8_synapse_B Leuk(loc~B,syn!1).Tc8(syn!1)

	Molecules Leuk4_synapse_BM Leuk(loc~BM,syn!1).Tc4(syn!1)
        Molecules Leuk8_synapse_BM Leuk(loc~BM,syn!1).Tc8(syn!1)


	Molecules CD34_CD123_B CD34(loc~B,a!1).A(cd3!1,cd123!2).CD123(a!2)
	Molecules CD38_CD123_B CD38(loc~B,a!1).A(cd3!1,cd123!2).CD123(a!2)
	Molecules CD34_CD123_BM CD34(loc~BM,a!1).A(cd3!1,cd123!2).CD123(a!2)
    Molecules CD38_CD123_BM CD38(loc~BM,a!1).A(cd3!1,cd123!2).CD123(a!2)
	Molecules active4_synapse_B Leuk(loc~B,syn!1).Tc4(syn!1,act~1)
    Molecules active8_synapse_B Leuk(loc~B,syn!1).Tc8(syn!1,act~1)
	Molecules active4_synapse_BM Leuk(loc~BM,syn!1).Tc4(syn!1,act~1)
    Molecules active8_synapse_BM Leuk(loc~BM,syn!1).Tc8(syn!1,act~1)
	Molecules active4_nonex_synapse_B Leuk(loc~B,syn!1).Tc4(syn!1,ex~0,act~1)
    Molecules active8_nonex_synapse_B Leuk(loc~B,syn!1).Tc8(syn!1,ex~0,act~1)
	Molecules active4_nonex_synapse_BM Leuk(loc~BM,syn!1).Tc4(syn!1,ex~0,act~1)
    Molecules active8_nonex_synapse_BM Leuk(loc~BM,syn!1).Tc8(syn!1,ex~0,act~1)
	# T-cell trafficking related 
	Molecules Tc4_ready_to_traffic Tc4(loc~B,syn,act~0,adh~1)
        Molecules Tc8_ready_to_traffic Tc8(loc~B,syn,act~0,adh~1)
        
	# Cytokines
    Molecules IFNg_B IFNg(loc~B)
    Molecules IL2_B IL2(loc~B)
    Molecules IL6_B IL6(loc~B)
    Molecules IL10_B IL10(loc~B)
    Molecules IFNg_BM IFNg(loc~BM)
    Molecules IL2_BM IL2(loc~BM)
    Molecules IL6_BM IL6(loc~BM)
    Molecules IL10_BM IL10(loc~BM)

end observables

begin functions
  # Compartment concentrations in ng/ml
  C_B() A_B()/V_B/NA*molwt*1e9
  C_P() A_P()/V_P/NA*molwt*1e9
  C_BM() A_BM()/V_BM/NA*molwt*1e9
  # Receptor numbers per cell
  CD34_Tc4_B() CD34_B/(Tc4_B + eps)
  CD38_Tc8_B() CD38_B/(Tc8_B + eps)
  CD34_Tc4_BM() CD34_BM/(Tc4_BM + eps)
  CD38_Tc8_BM() CD38_BM/(Tc8_BM + eps)
  CD123_Leuk_B() CD123_B/(Leuk_B + eps)
  CD123_Leuk_BM() CD123_BM/(Leuk_BM + eps)

  # Fraction of bound CD3
  fCD34bound_B() CD34_bound_B/(CD34_B + eps)
  fCD38bound_B() CD38_bound_B/(CD38_B + eps)
  fCD34bound_BM() CD34_bound_BM/(CD34_BM + eps)
  fCD38bound_BM() CD38_bound_BM/(CD38_BM + eps)
  # Synapse related
  r4_synapse_B() kf*rho123_init*CD34_B*(kp3*CD123_bound_B*CD34_free_B + kp123*CD123_free_B*CD34_bound_B)/ \
					(Tc4_B*(CD123_free_B + CD123_bound_B)*(CD34_free_B + CD34_bound_B) + eps)/V_B

  r8_synapse_B() kf*rho123_init*CD38_B*(kp3*CD123_bound_B*CD38_free_B + kp123*CD123_free_B*CD38_bound_B)/ \
					(Tc8_B*(CD123_free_B + CD123_bound_B)*(CD38_free_B + CD38_bound_B) + eps)/V_B
  r4_synapse_BM() kf*rho123_init*CD34_BM*(kp3*CD123_bound_BM*CD34_free_BM + kp123*CD123_free_BM*CD34_bound_BM)/ \
					(Tc4_BM*(CD123_free_BM + CD123_bound_BM)*(CD34_free_BM + CD34_bound_BM) + eps)/V_BM
  r8_synapse_BM() kf*rho123_init*CD38_BM*(kp3*CD123_bound_BM*CD38_free_BM + kp123*CD123_free_BM*CD38_bound_BM)/ \
					(Tc8_BM*(CD123_free_BM + CD123_bound_BM)*(CD38_free_BM + CD38_bound_BM) + eps)/V_BM
  
  r4_bridge_B() nb*kf*Leuk_free_B*Tc4_free_B*CD34_B*rho123_init/(Tc4_B*(CD34_free_B + CD34_bound_B)*(CD123_free_B + CD123_bound_B) + eps)/V_B
  r8_bridge_B() nb*kf*Leuk_free_B*Tc8_free_B*CD38_B*rho123_init/(Tc8_B*(CD38_free_B + CD38_bound_B)*(CD123_free_B + CD123_bound_B) + eps)/V_B

  r4_bridge_BM() nb*kf*Leuk_free_BM*Tc4_free_BM*CD34_BM*rho123_init/(Tc4_BM*(CD34_free_BM + CD34_bound_BM)*(CD123_free_BM + CD123_bound_BM) + eps)/V_BM
  r8_bridge_BM() nb*kf*Leuk_free_BM*Tc8_free_BM*CD38_BM*rho123_init/(Tc8_BM*(CD38_free_BM + CD38_bound_BM)*(CD123_free_BM + CD123_bound_BM) + eps)/V_BM

  n4_bridge_syn_B() CD34_CD123_B/(Leuk4_synapse_B + eps) 
  n8_bridge_syn_B() CD38_CD123_B/(Leuk8_synapse_B + eps) 

  n4_bridge_syn_BM() CD34_CD123_BM/(Leuk4_synapse_BM + eps)
  n8_bridge_syn_BM() CD38_CD123_BM/(Leuk8_synapse_BM + eps)

  b3() 0 # km123/(km3 + km123) # Fraction of bridges that dissociate into CD3.A + CD123

  r4_kill_B() k4_kill*active4_nonex_synapse_B/(Leuk4_synapse_B + eps) # Stable form: Does not use nb
  r8_kill_B() k8_kill*active8_nonex_synapse_B/(Leuk8_synapse_B + eps) # Stable form: Does not use nb
  r4_kill_BM() k4_kill*active4_nonex_synapse_BM/(Leuk4_synapse_BM + eps)
  r8_kill_BM() k8_kill*active8_nonex_synapse_BM/(Leuk8_synapse_BM + eps)

  r4_kill123_B() k4_kill*active4_nonex_synapse_B/(CD123_free_B + CD123_bound_B + eps)*(CD123_B/(Leuk_B + eps) - CD34_CD123_B/(Leuk4_synapse_B + eps))
  r8_kill123_B() k8_kill*active8_nonex_synapse_B/(CD123_free_B + CD123_bound_B + eps)*(CD123_B/(Leuk_B + eps) - CD38_CD123_B/(Leuk8_synapse_B + eps))

  r4_kill123_BM() k4_kill*active4_nonex_synapse_BM/(CD123_free_BM + CD123_bound_BM + eps)*(CD123_BM/(Leuk_BM + eps) - CD34_CD123_BM/(Leuk4_synapse_BM + eps))
  r8_kill123_BM() k8_kill*active8_nonex_synapse_BM/(CD123_free_BM + CD123_bound_BM + eps)*(CD123_BM/(Leuk_BM + eps) - CD38_CD123_BM/(Leuk8_synapse_BM + eps))


  r4_tox_syn_B() k4_tox*active4_synapse_B/(Leuk4_synapse_B + eps) # Stable form: Does not use nb
  r8_tox_syn_B() k8_tox*active8_synapse_B/(Leuk8_synapse_B + eps) # Stable form: Does not use nb

  r4_tox_syn_BM() k4_tox*active4_synapse_BM/(Leuk4_synapse_BM + eps) 
  r8_tox_syn_BM() k8_tox*active8_synapse_BM/(Leuk8_synapse_BM + eps)


  r4_tox3_syn_B() k4_tox*active4_synapse_B*(CD34_B/(Tc4_B + eps) - CD34_CD123_B/(Leuk4_synapse_B + eps))/(CD34_free_B + CD34_bound_B + eps)
  r8_tox3_syn_B() k8_tox*active8_synapse_B*(CD38_B/(Tc8_B + eps) - CD38_CD123_B/(Leuk8_synapse_B + eps))/(CD38_free_B + CD38_bound_B + eps)


  r4_tox3_syn_BM() k4_tox*active4_synapse_BM*(CD34_BM/(Tc4_BM + eps) - CD34_CD123_BM/(Leuk4_synapse_BM + eps))/(CD34_free_BM + CD34_bound_BM + eps)
  r8_tox3_syn_BM() k8_tox*active8_synapse_BM*(CD38_BM/(Tc8_BM + eps) - CD38_CD123_BM/(Leuk8_synapse_BM + eps))/(CD38_free_BM + CD38_bound_BM + eps)


  r4_tox_B() k4_tox*Tc4_free_act_B*CD34_B/Tc4_B/(CD34_bound_B + CD34_free_B + eps)
  r8_tox_B() k8_tox*Tc8_free_act_B*CD38_B/Tc8_B/(CD38_bound_B + CD38_free_B + eps)


  r4_tox_BM() k4_tox*Tc4_free_act_BM*CD34_BM/Tc4_BM/(CD34_bound_BM + CD34_free_BM + eps)
  r8_tox_BM() k8_tox*Tc8_free_act_BM*CD38_BM/Tc8_BM/(CD38_bound_BM + CD38_free_BM + eps)

  # Free receptors in non-synasped cells subject to homeostasis
  p_CD34_naive_B() Tc4_naive_B*rho3_init/(CD34_B-CD34_CD123_B)
  p_CD38_naive_B() Tc8_naive_B*rho3_init/(CD38_B-CD38_CD123_B)
  
  p_CD34_naive_BM() Tc4_naive_BM*rho3_init/(CD34_BM-CD34_CD123_BM)
  p_CD38_naive_BM() Tc8_naive_BM*rho3_init/(CD38_BM-CD38_CD123_BM)


  p_CD123_free_B() Leuk_free_B*rho123_init/(CD123_B-CD34_CD123_B - CD38_CD123_B)

  p_CD123_free_BM() Leuk_free_BM*rho123_init/(CD123_BM-CD34_CD123_BM - CD38_CD123_BM) 
  
  # T cell expansion  
  r4_tcb_B() k_tcb*rho3_init*Tc4_free_act_B/(CD34_free_B + CD34_bound_B + eps)
  r8_tcb_B() k_tcb*rho3_init*Tc8_free_act_B/(CD38_free_B + CD38_bound_B + eps)

  r4_tcb_BM() k_tcb*rho3_init*Tc4_free_act_BM/(CD34_free_BM + CD34_bound_BM + eps)
  r8_tcb_BM() k_tcb*rho3_init*Tc8_free_act_BM/(CD38_free_BM + CD38_bound_BM + eps)


  # T-cell migration
#  r_adh() k_migrate_B*CD3_bound_B/(CD3_bound_B + CD3_free_B + eps)
  cd34_bound_cell_B() CD34_bound_B/(Tc4_B + eps)
  cd38_bound_cell_B() CD38_bound_B/(Tc8_B + eps)

  cd34_bound_cell_BM() CD34_bound_BM/(Tc4_BM + eps)
  cd38_bound_cell_BM() CD38_bound_BM/(Tc8_BM + eps)


  r4_adh() k_migrate_B*(cd34_bound_cell_B()^2/(cd34_bound_cell_B()^2 + thresh_adh^2)) 
  r8_adh() k_migrate_B*(cd38_bound_cell_B()^2/(cd38_bound_cell_B()^2 + thresh_adh^2)) 


  r4_rev() k_migrate_BM*(thresh_rev^2/(cd34_bound_cell_BM()^2 + thresh_rev^2))
  r8_rev() k_migrate_BM*(thresh_rev^2/(cd38_bound_cell_BM()^2 + thresh_rev^2))
	  
end functions
begin reaction rules

# ***T cell homeostasis from logistic growth***
Tc4(loc~B,syn,act~0,ex~0,adh~0) <-> Tc4(loc~B,syn,act~0,ex~0,adh~0) + Tc4(loc~B,syn,act~0,ex~0,adh~0) k_tc4_log, k_tc4_log/(tc4_dens_B*V_B) 
Tc8(loc~B,syn,act~0,ex~0,adh~0) <-> Tc8(loc~B,syn,act~0,ex~0,adh~0) + Tc8(loc~B,syn,act~0,ex~0,adh~0) k_tc8_log, k_tc8_log/(tc8_dens_B*V_B) 
#
Tc4(loc~BM,syn,act~0,ex~0,adh~0) <-> Tc4(loc~BM,syn,act~0,ex~0,adh~0) + Tc4(loc~BM,syn,act~0,ex~0,adh~0) k_tc4_log, k_tc4_log/(tc4_dens_BM*V_BM)
Tc8(loc~BM,syn,act~0,ex~0,adh~0) <-> Tc8(loc~BM,syn,act~0,ex~0,adh~0) + Tc8(loc~BM,syn,act~0,ex~0,adh~0) k_tc8_log, k_tc8_log/(tc8_dens_BM*V_BM)

# CD3 receptor production
0-> CD34(loc~B,a) k_tc4_log*Tc4_naive_B*rho3_init
0-> CD38(loc~B,a) k_tc8_log*Tc8_naive_B*rho3_init


0-> CD34(loc~BM,a) k_tc4_log*Tc4_naive_BM*rho3_init
0-> CD38(loc~BM,a) k_tc8_log*Tc8_naive_BM*rho3_init


# CD3 receptor degradation
CD34(loc~B,a) -> 0 k_tc4_log/(tc4_dens_B*V_B)*Tc4_naive_B*p_CD34_naive_B()
CD38(loc~B,a) -> 0 k_tc8_log/(tc8_dens_B*V_B)*Tc8_naive_B*p_CD38_naive_B()

CD34(loc~B,a!1).A(cd3!1,cd123) -> A(cd3,cd123) k_tc4_log/(tc4_dens_B*V_B)*Tc4_naive_B*p_CD34_naive_B()
CD38(loc~B,a!1).A(cd3!1,cd123) -> A(cd3,cd123) k_tc8_log/(tc8_dens_B*V_B)*Tc8_naive_B*p_CD38_naive_B()

CD34(loc~BM,a) -> 0 k_tc4_log/(tc4_dens_BM*V_BM)*Tc4_naive_BM*p_CD34_naive_BM()
CD38(loc~BM,a) -> 0 k_tc8_log/(tc8_dens_BM*V_BM)*Tc8_naive_BM*p_CD38_naive_BM()

CD34(loc~BM,a!1).A(cd3!1,cd123) -> A(cd3,cd123) k_tc4_log/(tc4_dens_BM*V_BM)*Tc4_naive_BM*p_CD34_naive_BM()
CD38(loc~BM,a!1).A(cd3!1,cd123) -> A(cd3,cd123) k_tc8_log/(tc8_dens_BM*V_BM)*Tc8_naive_BM*p_CD38_naive_BM()
 
# ***Leuk homeostasis ***
# Logistic growth
Leuk(loc~B,syn) <-> Leuk(loc~B,syn) + Leuk(loc~B,syn) k_LeukB_log, k_LeukB_log/(Leuk_dens_B*V_B)
Leuk(loc~BM,syn) <-> Leuk(loc~BM,syn) + Leuk(loc~BM,syn) k_LeukBM_log, k_LeukBM_log/(Leuk_dens_BM*V_BM)
#  CD123 production
0->CD123(loc~B,a) k_LeukB_log*Leuk_free_B*rho123_init
0->CD123(loc~BM,a) k_LeukBM_log*Leuk_free_BM*rho123_init
# CD123 degradation
CD123(loc~B,a) -> 0 k_LeukB_log/(Leuk_dens_B*V_B)*Leuk_free_B*p_CD123_free_B()
CD123(loc~B,a!1).A(cd123!1,cd3) -> A(cd123,cd3) k_LeukB_log/(Leuk_dens_B*V_B)*Leuk_free_B*p_CD123_free_B()
CD123(loc~BM,a) -> 0 k_LeukBM_log/(Leuk_dens_BM*V_BM)*Leuk_free_BM*p_CD123_free_BM()
CD123(loc~BM,a!1).A(cd123!1,cd3) -> A(cd123,cd3) k_LeukBM_log/(Leuk_dens_BM*V_BM)*Leuk_free_BM*p_CD123_free_BM()

## Simple growth
#0 <-> Leuk(loc~B,syn) k_Leuk_syn_B, k_Leuk_deg_B
#0 <-> Leuk(loc~BM,syn) k_Leuk_syn_BM, k_Leuk_deg_BM
#
## Associated CD123 production and degradation
## Free receptors
#0 <-> CD123(loc~B,a) k_Leuk_syn_B*rho123_init, k_Leuk_deg_B*Leuk_free_B*p_CD123_free_B()
#0 <-> CD123(loc~BM,a) k_Leuk_syn_BM*rho123_init, k_Leuk_deg_BM*Leuk_free_BM*p_CD123_free_BM()
#
## Degradation of drug-bound receptors
#CD123(loc~B,a!1).A(cd123!1,cd3) -> A(cd123,cd3) k_Leuk_deg_B*Leuk_free_B*p_CD123_free_B()
#CD123(loc~BM,a!1).A(cd123!1,cd3) -> A(cd123,cd3) k_Leuk_deg_BM*Leuk_free_BM*p_CD123_free_BM()
 

# ***Drug PK ***
#  Infusion into blood
0 -> A(loc~B,cd3,cd123) doserate 
# Clearance from blood
A(loc~B,cd3,cd123) -> 0 CL/V_B
#  Distribution
A(loc~B,cd3,cd123) <-> A(loc~P,cd3,cd123) q_P/V_B,q_P/V_P
A(loc~B,cd3,cd123) <-> A(loc~BM,cd3,cd123) q_BM/V_B,q_BM/V_BM

# *** Drug-receptor interactions ***
#  Drug binding to CD3 
A(loc~B,cd3,cd123) + CD34(loc~B,a) <-> A(loc~B,cd3!1,cd123).CD34(loc~B,a!1) kp3/V_B, km3
A(loc~B,cd3,cd123) + CD38(loc~B,a) <-> A(loc~B,cd3!1,cd123).CD38(loc~B,a!1) kp3/V_B, km3


A(loc~BM,cd3,cd123) + CD34(loc~BM,a) <-> A(loc~BM,cd3!1,cd123).CD34(loc~BM,a!1) kp3/V_BM, km3
A(loc~BM,cd3,cd123) + CD38(loc~BM,a) <-> A(loc~BM,cd3!1,cd123).CD38(loc~BM,a!1) kp3/V_BM, km3


# Drug binding to CD123 
A(loc~B,cd3,cd123) + CD123(loc~B,a) <-> A(loc~B,cd3,cd123!1).CD123(loc~B,a!1) kp123/V_B, km123
A(loc~BM,cd3,cd123) + CD123(loc~BM,a) <-> A(loc~BM,cd3,cd123!1).CD123(loc~BM,a!1) kp123/V_BM, km123

# ***Synapse formation ***
# Leuk binding to TC
Leuk(loc~B,syn) + Tc4(loc~B,syn) ->  Leuk(loc~B,syn!1).Tc4(loc~B,syn!1) r4_synapse_B()
Leuk(loc~B,syn) + Tc8(loc~B,syn) ->  Leuk(loc~B,syn!1).Tc8(loc~B,syn!1) r8_synapse_B()

Leuk(loc~BM,syn) + Tc4(loc~BM,syn)-> Leuk(loc~BM,syn!1).Tc4(loc~BM,syn!1) r4_synapse_BM()
Leuk(loc~BM,syn) + Tc8(loc~BM,syn)-> Leuk(loc~BM,syn!1).Tc8(loc~BM,syn!1) r8_synapse_BM()

# Formation of drug bridges between cells 
CD34(loc~B,a) + A(loc~B,cd3,cd123!2).CD123(a!2) -> CD34(loc~B,a!1).A(loc~B,cd3!1,cd123!2).CD123(a!2) kp3*r4_bridge_B()
CD38(loc~B,a) + A(loc~B,cd3,cd123!2).CD123(a!2) -> CD38(loc~B,a!1).A(loc~B,cd3!1,cd123!2).CD123(a!2) kp3*r8_bridge_B()


CD34(loc~B,a!1).A(cd3!1,cd123) + CD123(loc~B,a) -> CD34(loc~B,a!1).A(cd3!1,cd123!2).CD123(loc~B,a!2)  kp123*r4_bridge_B()
CD38(loc~B,a!1).A(cd3!1,cd123) + CD123(loc~B,a) -> CD38(loc~B,a!1).A(cd3!1,cd123!2).CD123(loc~B,a!2)  kp123*r8_bridge_B()

CD34(loc~BM,a) + A(loc~BM,cd3,cd123!2).CD123(a!2) -> CD34(loc~BM,a!1).A(loc~BM,cd3!1,cd123!2).CD123(a!2) kp3*r4_bridge_BM()
CD38(loc~BM,a) + A(loc~BM,cd3,cd123!2).CD123(a!2) -> CD38(loc~BM,a!1).A(loc~BM,cd3!1,cd123!2).CD123(a!2) kp3*r8_bridge_BM()


CD34(loc~BM,a!1).A(cd3!1,cd123) + CD123(loc~BM,a) -> CD34(loc~BM,a!1).A(cd3!1,cd123!2).CD123(loc~BM,a!2)  kp123*r4_bridge_BM()
CD38(loc~BM,a!1).A(cd3!1,cd123) + CD123(loc~BM,a) -> CD38(loc~BM,a!1).A(cd3!1,cd123!2).CD123(loc~BM,a!2)  kp123*r8_bridge_BM()


# *** T cell activation ***
Tc4(syn!+,act~0) -> Tc4(syn!+,act~1) k4_act
Tc8(syn!+,act~0) -> Tc8(syn!+,act~1) k8_act

# ** T cell exhaustion ***
Tc4(syn,act~1,ex~0) -> Tc4(syn,act~1,ex~1) k4_ex
Tc8(syn,act~1,ex~0) -> Tc8(syn,act~1,ex~1) k8_ex


# *** Killing ***
# First-order loss of synapses and associated receptors 
Tc4(loc~B,syn!1,act~1,ex~0).Leuk(syn!1) -> Tc4(loc~B,syn,act~1,ex~0) k4_kill 
Tc8(loc~B,syn!1,act~1,ex~0).Leuk(syn!1) -> Tc8(loc~B,syn,act~1,ex~0) k8_kill 

Tc4(loc~BM,syn!1,act~1,ex~0).Leuk(syn!1) -> Tc4(loc~BM,syn,act~1,ex~0) k4_kill
Tc8(loc~BM,syn!1,act~1,ex~0).Leuk(syn!1) -> Tc8(loc~BM,syn,act~1,ex~0) k8_kill

# Loss of bridged CD123 
CD34(loc~B,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD34(loc~B,a) (1-b3())*r4_kill_B()
CD38(loc~B,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD38(loc~B,a) (1-b3())*r8_kill_B()

CD34(loc~B,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD34(loc~B,a!1).A(cd3!1,cd123) b3()*r4_kill_B()
CD38(loc~B,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD38(loc~B,a!1).A(cd3!1,cd123) b3()*r8_kill_B()

CD34(loc~BM,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD34(loc~BM,a) (1-b3())*r4_kill_BM()
CD38(loc~BM,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD38(loc~BM,a) (1-b3())*r8_kill_BM()

CD34(loc~BM,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD34(loc~BM,a!1).A(cd3!1,cd123) b3()*r4_kill_BM()
CD38(loc~BM,a!1).A(cd3!1,cd123!2).CD123(a!2) -> CD38(loc~BM,a!1).A(cd3!1,cd123) b3()*r8_kill_BM()


# Treating compartments separately when handling non-bridged CD123
A(loc~B,cd3,cd123!1).CD123(a!1) -> 0 r4_kill123_B() 
A(loc~B,cd3,cd123!1).CD123(a!1) -> 0 r8_kill123_B() 

A(loc~BM,cd3,cd123!1).CD123(a!1) -> 0 r4_kill123_BM() 
A(loc~BM,cd3,cd123!1).CD123(a!1) -> 0 r8_kill123_BM() 

CD123(loc~B,a) -> 0 r4_kill123_B() 
CD123(loc~B,a) -> 0 r8_kill123_B() 

CD123(loc~BM,a) -> 0 r4_kill123_BM() 
CD123(loc~BM,a) -> 0 r8_kill123_BM() 



# *** T cell toxicity ***
# Toxicity to synapsed T cells 
Tc4(loc~B,syn!1,act~1).Leuk(syn!1) -> Leuk(loc~B,syn) k4_tox
Tc8(loc~B,syn!1,act~1).Leuk(syn!1) -> Leuk(loc~B,syn) k8_tox


Tc4(loc~BM,syn!1,act~1).Leuk(syn!1) -> Leuk(loc~BM,syn) k4_tox
Tc8(loc~BM,syn!1,act~1).Leuk(syn!1) -> Leuk(loc~BM,syn) k8_tox


# Loss of bridged CD3
CD34(a!1).A(cd3!1,cd123!2).CD123(loc~B,a!2) -> A(cd3,cd123!2).CD123(loc~B,a!2) (1-b3())*r4_tox_syn_B()
CD38(a!1).A(cd3!1,cd123!2).CD123(loc~B,a!2) -> A(cd3,cd123!2).CD123(loc~B,a!2) (1-b3())*r8_tox_syn_B()


CD34(a!1).A(cd3!1,cd123!2).CD123(loc~B,a!2) -> CD123(loc~B,a) b3()*r4_tox_syn_B()
CD38(a!1).A(cd3!1,cd123!2).CD123(loc~B,a!2) -> CD123(loc~B,a) b3()*r8_tox_syn_B()

CD34(a!1).A(cd3!1,cd123!2).CD123(loc~BM,a!2) -> A(cd3,cd123!2).CD123(loc~BM,a!2) (1-b3())*r4_tox_syn_BM()
CD38(a!1).A(cd3!1,cd123!2).CD123(loc~BM,a!2) -> A(cd3,cd123!2).CD123(loc~BM,a!2) (1-b3())*r8_tox_syn_BM()


CD34(a!1).A(cd3!1,cd123!2).CD123(loc~BM,a!2) -> CD123(loc~BM,a) b3()*r4_tox_syn_BM()
CD38(a!1).A(cd3!1,cd123!2).CD123(loc~BM,a!2) -> CD123(loc~BM,a) b3()*r8_tox_syn_BM()


# Loss of non-bridged CD3 
CD34(a!1).A(loc~B,cd3!1,cd123) -> 0 r4_tox3_syn_B()
CD38(a!1).A(loc~B,cd3!1,cd123) -> 0 r8_tox3_syn_B()


CD34(a!1).A(loc~BM,cd3!1,cd123) -> 0 r4_tox3_syn_BM()
CD38(a!1).A(loc~BM,cd3!1,cd123) -> 0 r8_tox3_syn_BM()


CD34(loc~B,a) -> 0 r4_tox3_syn_B()
CD38(loc~B,a) -> 0 r8_tox3_syn_B()


CD34(loc~BM,a) -> 0 r4_tox3_syn_BM()
CD38(loc~BM,a) -> 0 r8_tox3_syn_BM()


# Also want to deplete active T cells that are not synapsed - need corrsponding loss of CD3 and CD3.A.
# Toxicity to un-synapsed T cells
Tc4(loc~B,syn,act~1)-> 0 k4_tox
Tc8(loc~B,syn,act~1)-> 0 k8_tox

Tc4(loc~BM,syn,act~1)-> 0 k4_tox
Tc8(loc~BM,syn,act~1)-> 0 k8_tox

# Loss of non-bridged CD3 
CD34(loc~B,a!1).A(cd3!1,cd123) -> 0 r4_tox_B() 
CD38(loc~B,a!1).A(cd3!1,cd123) -> 0 r8_tox_B() 

CD34(loc~BM,a!1).A(cd3!1,cd123) -> 0 r4_tox_BM()
CD38(loc~BM,a!1).A(cd3!1,cd123) -> 0 r8_tox_BM()

CD34(loc~B,a) -> 0 r4_tox_B()
CD38(loc~B,a) -> 0 r8_tox_B()


CD34(loc~BM,a) -> 0 r4_tox_BM()
CD38(loc~BM,a) -> 0 r8_tox_BM()

# ***T cell expansion***
# Only free active T cells replicate
# New CD3 comes from both bound and free CD3, so replication occurs even when CD3 is saturated 
Tc4(loc~B,syn,act~1,ex~0) -> Tc4(loc~B,syn,act~1,ex~0) + Tc4(loc~B,syn,act~1,ex~0,adh~0) k_tcb
Tc8(loc~B,syn,act~1,ex~0) -> Tc8(loc~B,syn,act~1,ex~0) + Tc8(loc~B,syn,act~1,ex~0,adh~0) k_tcb
Tc4(loc~B,syn,act~1,ex~1) -> Tc4(loc~B,syn,act~1,ex~1) + Tc4(loc~B,syn,act~1,ex~1,adh~0) k_tcb
Tc8(loc~B,syn,act~1,ex~1) -> Tc8(loc~B,syn,act~1,ex~1) + Tc8(loc~B,syn,act~1,ex~1,adh~0) k_tcb


CD34(loc~B,a) -> CD34(loc~B,a) + CD34(loc~B,a) r4_tcb_B()
CD38(loc~B,a) -> CD38(loc~B,a) + CD38(loc~B,a) r8_tcb_B()


CD34(loc~B,a!1).A(cd3!1,cd123) -> CD34(loc~B,a!1).A(cd3!1,cd123) + CD34(loc~B,a) r4_tcb_B()
CD38(loc~B,a!1).A(cd3!1,cd123) -> CD38(loc~B,a!1).A(cd3!1,cd123) + CD38(loc~B,a) r8_tcb_B()


Tc4(loc~BM,syn,act~1,ex~0) -> Tc4(loc~BM,syn,act~1,ex~0) + Tc4(loc~BM,syn,act~1,ex~0,adh~0) k_tcb
Tc8(loc~BM,syn,act~1,ex~0) -> Tc8(loc~BM,syn,act~1,ex~0) + Tc8(loc~BM,syn,act~1,ex~0,adh~0) k_tcb
Tc4(loc~BM,syn,act~1,ex~1) -> Tc4(loc~BM,syn,act~1,ex~1) + Tc4(loc~BM,syn,act~1,ex~1,adh~0) k_tcb
Tc8(loc~BM,syn,act~1,ex~1) -> Tc8(loc~BM,syn,act~1,ex~1) + Tc8(loc~BM,syn,act~1,ex~1,adh~0) k_tcb


CD34(loc~BM,a) -> CD34(loc~BM,a) + CD34(loc~BM,a) r4_tcb_BM()
CD38(loc~BM,a) -> CD38(loc~BM,a) + CD38(loc~BM,a) r8_tcb_BM()


CD34(loc~BM,a!1).A(cd3!1,cd123) -> CD34(loc~BM,a!1).A(cd3!1,cd123) + CD34(loc~BM,a) r4_tcb_BM()
CD38(loc~BM,a!1).A(cd3!1,cd123) -> CD38(loc~BM,a!1).A(cd3!1,cd123) + CD38(loc~BM,a) r8_tcb_BM()


# ***T-cell trafficking; we send them to BM!***
# T_cell gets ready to traffic 
Tc4(loc~B,syn,act~0,adh~0) -> Tc4(loc~B,syn,act~0,adh~1) r4_adh() 
Tc8(loc~B,syn,act~0,adh~0) -> Tc8(loc~B,syn,act~0,adh~1) r8_adh() 


# T cells and associated CD3 move to BM
Tc4(loc~B,syn,act~0,adh~1) -> Tc4(loc~BM,syn,act~0,adh~1) k_migrate_B # the ready-to-traffic T-cells move to BM where they become "naive" T-cells.
Tc8(loc~B,syn,act~0,adh~1) -> Tc8(loc~BM,syn,act~0,adh~1) k_migrate_B # the ready-to-traffic T-cells move to BM where they become "naive" T-cells.


CD34(loc~B,a) -> CD34(loc~BM,a) k_migrate_B*Tc4_ready_to_traffic*CD34_B/(Tc4_B*(CD34_free_B+CD34_bound_B)+eps)
CD38(loc~B,a) -> CD38(loc~BM,a) k_migrate_B*Tc8_ready_to_traffic*CD38_B/(Tc8_B*(CD38_free_B+CD38_bound_B)+eps)


CD34(loc~B,a!1).A(loc~B,cd3!1,cd123) -> CD34(loc~BM,a!1).A(loc~BM,cd3!1,cd123) k_migrate_B*Tc4_ready_to_traffic*CD34_B/(Tc4_B*(CD34_free_B+CD34_bound_B)+eps)
CD38(loc~B,a!1).A(loc~B,cd3!1,cd123) -> CD38(loc~BM,a!1).A(loc~BM,cd3!1,cd123) k_migrate_B*Tc8_ready_to_traffic*CD38_B/(Tc8_B*(CD38_free_B+CD38_bound_B)+eps)

#  T cells and CD3 return to blood from BM
Tc4(loc~BM,syn,adh~1) -> Tc4(loc~B,syn,adh~0) r4_rev() # Return rate proportion to fraction of free CD3 on trafficked cells
Tc8(loc~BM,syn,adh~1) -> Tc8(loc~B,syn,adh~0) r8_rev() # Return rate proportion to fraction of free CD3 on trafficked cells


CD34(loc~BM,a) -> CD34(loc~B,a) r4_rev()*Tc4_free_adh_BM*CD34_BM/(Tc4_BM*(CD34_free_BM+CD34_bound_BM)+eps)
CD38(loc~BM,a) -> CD38(loc~B,a) r8_rev()*Tc8_free_adh_BM*CD38_BM/(Tc8_BM*(CD38_free_BM+CD38_bound_BM)+eps)


CD34(loc~BM,a!1).A(loc~BM,cd3!1,cd123) -> CD34(loc~B,a!1).A(loc~B,cd3!1,cd123) r4_rev()*Tc4_free_adh_BM*CD34_BM/(Tc4_BM*(CD34_free_BM+CD34_bound_BM)+eps)
CD38(loc~BM,a!1).A(loc~BM,cd3!1,cd123) -> CD38(loc~B,a!1).A(loc~B,cd3!1,cd123) r8_rev()*Tc8_free_adh_BM*CD38_BM/(Tc8_BM*(CD38_free_BM+CD38_bound_BM)+eps)


# ***Receptor trafficking***
# CD3 synthesis -----15.52
0 -> CD34(loc~B,a) kb3*Tc4_B
0 -> CD38(loc~B,a) kb3*Tc8_B


0 -> CD34(loc~BM,a) kb3*Tc4_BM
0 -> CD38(loc~BM,a) kb3*Tc8_BM


# Free CD3 degradation
CD34(a) -> 0 kd3
CD38(a) -> 0 kd3


# Bound CD3 degradation with drug
CD34(a!1).A(cd3!1,cd123) -> 0 ki3
CD38(a!1).A(cd3!1,cd123) -> 0 ki3

#cytokine reactions

# IFNg secretion and degradation
Tc4(loc~B,act~1,ex~0) -> Tc4(loc~B,act~1,ex~0) + IFNg(loc~B) kf_ifng
Tc8(loc~B,act~1,ex~0) -> Tc8(loc~B,act~1,ex~0) + IFNg(loc~B) kf_ifng
IFNg(loc~B) -> 0  kr_ifng

Tc4(loc~BM,act~1,ex~0) -> Tc4(loc~BM,act~1,ex~0) + IFNg(loc~BM) kf_ifng
Tc8(loc~BM,act~1,ex~0) -> Tc8(loc~BM,act~1,ex~0) + IFNg(loc~BM) kf_ifng
IFNg(loc~BM) -> 0  kr_ifng

# IL2 secretion and degradation
Tc4(loc~B,act~1,ex~0) -> Tc4(loc~B,act~1,ex~0) + IL2(loc~B) kf_il2
Tc8(loc~B,act~1,ex~0) -> Tc8(loc~B,act~1,ex~0) + IL2(loc~B) kf_il2
IL2(loc~B) -> 0  kr_il2

Tc4(loc~BM,act~1,ex~0) -> Tc4(loc~BM,act~1,ex~0) + IL2(loc~BM) kf_il2
Tc8(loc~BM,act~1,ex~0) -> Tc8(loc~BM,act~1,ex~0) + IL2(loc~BM) kf_il2
IL2(loc~BM) -> 0  kr_il2

# IL2 consumption by T cells -- not included for now
#IL2(loc~B) -> 0 kc_il2*(Tc4_free_act_B+Tc8_free_act_B)/(Kc_il2 + IL2)
#IL2(loc~BM) -> 0 kc_il2*(Tc4_free_act_BM+Tc8_free_act_BM)/(Kc_il2 + IL2)


# IL6 secretion and degradation
Tc4(loc~B,act~1,ex~0) -> Tc4(loc~B,act~1,ex~0) + IL6(loc~B) kf_il6
Tc8(loc~B,act~1,ex~0) -> Tc8(loc~B,act~1,ex~0) + IL6(loc~B) kf_il6
IL6(loc~B) -> 0  kr_il6

Tc4(loc~BM,act~1,ex~0) -> Tc4(loc~BM,act~1,ex~0) + IL6(loc~BM) kf_il6
Tc8(loc~BM,act~1,ex~0) -> Tc8(loc~BM,act~1,ex~0) + IL6(loc~BM) kf_il6
IL6(loc~BM) -> 0  kr_il6

# IL10 secretion by synapsed CD123 cells and degradation
Leuk(loc~B,syn!+) -> Leuk(loc~B,syn!+) + IL10(loc~B) kf_il10
IL10(loc~B) -> 0  kr_il10

Leuk(loc~BM,syn!+) -> Leuk(loc~BM,syn!+) + IL10(loc~BM) kf_il10
IL10(loc~BM) -> 0  kr_il10


end reaction rules
end model


generate_network({overwrite=>1});

writeSBML();

setParameter("CL",3.832473e+03)
setParameter("Q",5.767320e+03)
setParameter("k_tc4_log",3.603764e-01)
setParameter("k_tc8_log",2.575366e+00)
setParameter("k_LeukB_log",0.604855e+01)
setParameter("k_LeukBM_log",2.586583e+01)
setParameter("kf",4.358954e+00)
setParameter("nb",1.314861e+01)
setParameter("k4_act",1.586623e+02)
setParameter("k8_act",2.481135e+02)
setParameter("k_tcb",5.3e-2)
setParameter("k4_ex",8.164408e-02)
setParameter("k8_ex",11.166727e-02)
setParameter("k4_kill",1.552178e+01)
setParameter("k8_kill",3.309275e+01)
setParameter("k4_tox",1.232082e-01)
setParameter("k8_tox",1.232634e-01)
setParameter("k_adh",2.747248e+02)
setParameter("k_migrate_B",8.610515e+03)
setParameter("k_migrate_BM",4.214208e-01)
setParameter("kf_ifng",9.798275e+05)
setParameter("kr_ifng",2.174206e+01)
setParameter("kf_il2",1.138883e+05)
setParameter("kr_il2",10.095219e+00)
setParameter("kf_il6",2.140300e+06)
setParameter("kr_il6",2.399406e+02)
setParameter("kf_il10",36.600955e+05)
setParameter("kr_il10",0.530465e+01)

setParameter("dose",0.1)
simulate({method=>"ode",t_end=>1/24,n_steps=>20,print_functions=>1,rtol=>1e-12,atol=>1e-12})
setParameter("dose",0)
simulate({method=>"ode",t_end=>7,continue=>1,\
          n_steps=>140,\
          print_functions=>1,rtol=>1e-12,atol=>1e-12})

setParameter("dose",1.0)
simulate({method=>"ode",t_end=>169/24,n_steps=>20, continue=>1,print_functions=>1,rtol=>1e-12,atol=>1e-12})
setParameter("dose",0)
simulate({method=>"ode",t_end=>14,continue=>1,\
          n_steps=>140,\
          print_functions=>1,rtol=>1e-12,atol=>1e-12})

setParameter("dose",5.0)
simulate({method=>"ode",t_end=>337/24,n_steps=>20, continue=>1,print_functions=>1,rtol=>1e-12,atol=>1e-12})
setParameter("dose",0)
simulate({method=>"ode",t_end=>21,continue=>1,\
          n_steps=>140,\
          print_functions=>1,rtol=>1e-12,atol=>1e-12})    


